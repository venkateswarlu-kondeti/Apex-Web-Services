@isTest
private class CredentialVerificationServiceTest {
    @testSetup
    static void setupData(){
        Contact contact=new Contact(LastName='Test Contact');
        insert contact;
        Certification__c certification=new Certification__c(Name='Test Certification', isActive__c=true);
        insert certification;
        Date today=Date.today();
        Contact_Certification__c contactCertification=new Contact_Certification__c(isActive__c=true, Contact__c=contact.Id, Certification__c=certification.Id, Issue_Date__c=today);
        insert contactCertification;
    }
    
    @isTest
    static void testValidCertification(){
        Contact con=[SELECT Id,LastName FROM Contact LIMIT 1];
        Contact_Certification__c conCert=[SELECT Id,isActive__c,Name FROM Contact_Certification__c WHERE Contact__c=:con.Id LIMIT 1];
        Test.startTest();
        String result=CredentialVerificationService.verifyCredential(con.LastName,conCert.Name);
        Test.stopTest();
        System.assertEquals('Valid',result);
    }
    
    @isTest
    static void testNeedsRenewal(){
        Contact con=[SELECT Id,LastName FROM Contact LIMIT 1];
        Contact_Certification__c conCert=[SELECT Id,isActive__c,Name FROM Contact_Certification__c WHERE Contact__c=:con.Id LIMIT 1];
        conCert.isActive__c=false;
        update conCert;
        Test.startTest();
        String result=CredentialVerificationService.verifyCredential(con.LastName,conCert.Name);
        Test.stopTest();
        System.assertEquals('Needs Renewal',result);
    }
    
    @isTest
    static void testNoContactNameFound(){
        String lastName='Test';
        Contact_Certification__c conCert=[SELECT Id,isActive__c,Name FROM Contact_Certification__c LIMIT 1];
        Test.startTest();
        String result=CredentialVerificationService.verifyCredential(lastName,conCert.Name);
        Test.stopTest();
        System.assertEquals('No record found',result);
    }
    
    @isTest
    static void testNoContactCertificationNameFound(){
        Contact con=[SELECT Id,LastName FROM Contact LIMIT 1];
        String conCertName='ABCD';
        Test.startTest();
        String result=CredentialVerificationService.verifyCredential(con.LastName,conCertName);
        Test.stopTest();
        System.assertEquals('No record found',result);
    }
}
